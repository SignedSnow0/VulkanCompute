uint hash(uint x){
    x += ( x << 10u);
    x ^= ( x >> 6u);
    x += ( x << 3u);
    x ^= ( x >> 11u);
    x += ( x << 15u);
    return x;  
}

uint hash( uvec2 v ) { return hash( v.x ^ hash(v.y) ); }
uint hash( uvec3 v ) { return hash( v.x ^ hash(v.y) ^ hash(v.z) ); }
uint hash( uvec4 v ) { return hash( v.x ^ hash(v.y) ^ hash(v.z) ^ hash(v.w) ); }

float floatConstruct( uint m ) {
    const uint ieeeMantissa = 0x007FFFFFu; // binary32 mantissa bitmask
    const uint ieeeOne      = 0x3F800000u; // 1.0 in IEEE binary32

    m &= ieeeMantissa;                     // Keep only mantissa bits (fractional part)
    m |= ieeeOne;                          // Add fractional part to 1.0

    float  f = uintBitsToFloat( m );       // Range [1:2]
    return f - 1.0;                        // Range [0:1]
}


float random( float x ) { return floatConstruct(hash(floatBitsToUint(x))); }
float random( vec2  v ) { return floatConstruct(hash(floatBitsToUint(v))); }
float random( vec3  v ) { return floatConstruct(hash(floatBitsToUint(v))); }
float random( vec4  v ) { return floatConstruct(hash(floatBitsToUint(v))); }

vec3 randomOnSphere(inout vec3 seed) {
    vec3 vec = vec3(random(seed++), random(seed++), random(seed++));
    return normalize(vec);
}

vec3 randomOnHemisphere(inout vec3 seed, vec3 normal) {
    vec3 vec = randomOnSphere(seed);
    if(dot(normal, vec) < 0.0) {
        return -vec;
    }else {
        return vec;
    }
}