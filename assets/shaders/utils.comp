#ifndef UTILS_COMP
#define UTILS_COMP

const float PI = 3.14159265359f;
const float TWO_PI = 6.28318530718f;
const float INV_PI = .31830988618f;
const float INV_TWO_PI = .15915494309f;
const float EPSILON = .000001f;
const float MAX_FLOAT = 3.402823466e+38f;
const vec3 UP = vec3(0.f,1.f,0.f);

float random(inout uint state) {
    state = state * 747796405u + 2891336453u;
    uint result=((state >> ((state >> 28u) + 4u)) ^ state) * 277803737u;
    result=(result >> 22u) ^ result;
    return float(result) / 4294967295.f;
}

float random(inout uint state,float min,float max) {
    return min + (max - min) * random(state);
}

float randomNormalDistribution(inout uint state) {
    float theta = TWO_PI * random(state);
    float rho = sqrt(-2.f * log(random(state)));
    return rho * cos(theta);
}

vec3 randomOnSphere(inout uint state) {
    return normalize(vec3(randomNormalDistribution(state),
    randomNormalDistribution(state),
    randomNormalDistribution(state)));
}

vec3 randomOnHemisphere(inout uint state,vec3 normal) {
    vec3 dir = randomOnSphere(state);
    return dir * sign(dot(normal,dir));
}
#endif