#version 460

struct Ray {
    vec3 origin;
    vec3 direction;
};

struct RayHit {
    vec3 position;
    vec3 normal;
    float distance;
};

struct Sphere {
    vec3 center;
    float radius;
    vec3 color;
};

layout(rgba8, set = 0, binding = 0) uniform image2D framebuffer;

layout(set = 0, binding = 1) uniform Camera {
    vec3 position;
    vec3 forward;
    vec3 up;
} camera;

layout(set = 0, binding = 2) uniform Settings {
    int debugView;
    float time;
} settings;

bool intersectSphere(Ray ray, Sphere sphere, out RayHit hit) {
    vec3 oc = ray.origin - sphere.center;
    float a = dot(ray.direction, ray.direction);
    float b = 2.0 * dot(oc, ray.direction);
    float c = dot(oc, oc) - sphere.radius * sphere.radius;
    float discriminant = b * b - 4.0 * a * c;
    if (discriminant < 0.0) {
        return false;
    } else {
        hit.distance = (-b - sqrt(discriminant)) / (2.0 * a);
        hit.position = ray.origin + hit.distance * ray.direction;
        hit.normal = normalize(hit.position - sphere.center);

        return true;
    }
}

layout(local_size_x = 8, local_size_y = 8) in;
void main()
{
    ivec2 pixelCoords = ivec2(gl_GlobalInvocationID.xy);
    ivec2 imageSize = imageSize(framebuffer);

    if (pixelCoords.x >= imageSize.x || pixelCoords.y >= imageSize.y) {
        return;
    }

    vec2 uv = (vec2(pixelCoords) + vec2(0.5)) / vec2(imageSize);
    uv = uv * 2.0 - 1.0;
    uv.x *= float(imageSize.x) / float(imageSize.y);
    uv.y *= -1.0;

    Ray ray;
    ray.origin = camera.position;
    ray.direction = normalize(vec3(uv, -1.0));

    Sphere sphere;
    sphere.center = vec3(0.0, 0.0, -3.0);
    sphere.radius = 1.0;
    sphere.color = vec3(1.0, 0.0, 0.0);

    vec3 pixelColor = vec3(0.5, 0.7, 1.0);

    vec3 lightDir = normalize(vec3(-0.5, 1.0, 0.5));

    RayHit hit;
    if (intersectSphere(ray, sphere, hit)) {
        float diffuse = max(dot(hit.normal, lightDir), 0.0);
        pixelColor = sphere.color * diffuse * (sin(settings.time) * 0.5 + 0.5);

        if (settings.debugView == 1) {
            pixelColor = hit.normal * 0.5 + 0.5;
        } else if (settings.debugView == 2) {
            float depth = hit.distance / 10.0;
            pixelColor = vec3(depth);
        }
    }

    imageStore(framebuffer, pixelCoords, vec4(pixelColor, 1.0));
}
